// Evol scripts.
// Authors:
//    Qwerty Dragon
//    Reid
// Description:
//    Pervert and arrogant sailor.

000-1.gat,86,99,0,1	script	Silvio	312;2,{
    mesn;
    mesq l("Hey!");
    next;
    mesq g(l("How are you doing, cutie?"),
           l("How are you doing, dude?"));
    next;

    menu
        l("Hello... Do I know you?"), -,
        rif(getq(ShipQuests_Gugli) > 0, l("Fine, could you please tell me where I can find Gugli's sailors?")), L_Sailors,
        l("Uhm, bye."), L_Quit;

    mes "";
    mesn;
    mesq g(l("Hehe, no. But I'm sure a fox like you would love to meet a catch like me!"),
           l("Hehe, no. But I'm sure a noob like you would like to meet an idol like me."));
    next;
    mesq g(l("So, which good wind brought you here? Were you on your raft to meet me? Or is it because you've heard of my feats on Artis?"),
           l("So, which good wind brought you here? Were you on your raft to meet my Julia? Or is it because you wanted to see the beautiful waitress' at Artis?"));
    next;

    menu
        g(l("You're right, it's about you."),
          l("You're right, it's about Julia.")), L_Julia,
        l("What is Artis?"), L_Artis,
        l("None of them?"), -;

    mes "";
    mesn;
    mesq g(l("My beauty, I was not always a sailor, you know, I was once an important individual!"),
           l("My friend, I was not always a sailor, you know, I was once an important individual!"));
    next;
    mesq l("Before...");
    next;

    setcamnpc "Gugli";
    mes "";
    mesn "Gugli";
    mesq l("Hey, don't worry about him, it's one of the first days in a long time that we can just chill out.");
    next;
    mesq l("Maybe he over did it with the bottle today to celebrate that!");
    next;
    restorecam;

    menu
        l("He's funny, it's not a problem."), -,
        l("Ok, I will leave him alone."), L_ForeverAlone;

    setcamnpc "Gugli";
    mes "";
    mesn "Gugli";
    mesq l("Oh alright, nevermind then.");
    next;
    restorecam;

    mesn;
    mesq l("Ah... Gugli... He's too young to understand our conversation.");
    next;
    mesq l("So, what was I saying?");
    next;

    menu
        l("You told me that you 'were' important."), L_VeryImportantNoob,
        l("You mentioned the quality of your wine."), -;

    mes "";
    mesn;
    mesq l("Oh really? How could I forget something as important as that?!");
    next;
    mesq l("I think that my wine has a great quality! I'm on my second bottle and I already... What were we talking about again?");

    close;

L_VeryImportantNoob:
    mes "";
    mesn;
    mesq l("It's true!");
    next;
    mesq l("But I can't tell you anything about it, I'm sorry.") ;
    next;

    setcamnpc "Gugli";
    mes "";
    mesn "Gugli";
    mesq l("Hey Silvio, it's your turn to carry the package, go!");
    next;
    restorecam;

    mesn;
    mesq g(l("Duty calls me, *hic*, see you later honey."),
           l("Duty calls me, *hic*, see you later dude."));

    close;

L_Julia:
    mes "";
    mesn;
    mesq g(l("You honor me, my beauty, but I'm already taken by Julia!"),
           l("Not to kill your dream, but... Julia is mine!"));
    next;
    mesq l("She said she will accept me when pious will have teeth. It's just a matter of time, you see?");
    next;

    menu
        l("I see. Bye!"), L_Quit,
        l("Oh look, there's a piou behind you!"), L_Piou;

L_Piou:
    mes "";
    mesn;
    mesq l("Seriously?!");
    next;
    mesq l("Hurry, hurry! We need to check it's teeth!");

    close;

L_Artis:
    mes "";
    mesn;
    mesq l("This is a nice place... There are some nice chicks...");
    next;
    mesq l("The elven from the library firted with me a while ago, so has Enora from the Warrior Guild.");
    next;
    mesq l("Oh, and Olga from the market place as well!");
    next;
    mesq l("You should look at them when we arrive at the port, they are worth it, hehe...");

    callfunc "GoodBye";

L_ForeverAlone:
    mes "";
    mesn "Narrator";
    mes col(l("Silvio starts to speak to his bottle, you leave, letting him have a private conversation."), 9);

    close;

L_Sailors:
    mes "";
    mesn;
    mesq l("What? Why? They aren't sexier than me, why do you want to see them?");
    next;
    mesq l("And what do you give me in exchange for these informations?");
    next;

    setcamnpc "Gugli";
    mes "";
    mesn "Gugli";
    mesq g(l("Don't bother her Silvio, or I'll have to tell her that you cry like a baby when you hear the thunder in the night."),
           l("Don't bother him Silvio, or I'll have to tell him that you cry like a baby when you hear the thunder in the night."));
    next;
    restorecam;

    mesn;
    mesq l("I...");
    next;
    mesq l("How could you... We said we wouldn't talk about this again...");
    next;
    mesq l("Tibbo went to the south-east of the island, others are gone to the north, and I think that Gulukan is not so far from here.");
    next;
    mesq l("But anyway, take the road which goes to the north to find everybody.");
    next;
    mesq l("Now, leave me alone...");
    next;

    menu
        l("See you."), L_Quit,
        l("*Imitate a thunder's sound.*"), -;

    closedialog;

    npctalk3 l("Aaaaaahhhhhh!");

    close;

L_Quit:
    closedialog;

    npctalk3 l("Give me a kiss before you say goodbye!");

    close;
}

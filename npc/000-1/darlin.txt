// Evol scripts.
// Authors:
//    Alige
//    Reid
// Description:
//    Darlin talks from the top of the tall mast.
// Variable:
//    x   ShipQuests_TreasureChest
// Values:
//    0   Treasure Chest has not been opened yet.
//    1   Treasure Chest has been opened.


000-1.gat,89,95,0,1	script	Darlin	103;14,{

    set @q, getq(ShipQuests_TreasureChest);

    mesn;
    setcamnpc;
    mesq l("HEY! HEY YOU!");
    next;
    mesq l("CAN YOU HEAR ME?!");
    next;
    restorecam;

    menu
        l("Yes!"), -,
        l("No!"), l_Quit;

    setcamnpc;
    mes "";
    mesn;
    mesq l("GOOD!");
    next;
    mesq l("I WAS HERE WHEN THEY RESCUED YOU!");
    next;
    mesq l("DO YOU FEEL BETTER?!");
    next;
    restorecam;

    menu
        l("I do feel better!"), -,
        l("Why don't you come down to talk?"), l_GoDown;

    mes "";
    menu
        l("And you, how are you doing?"), -,
        l("But I need to go, see you!"), l_Bye;

    setcamnpc;
    mes "";
    mesn;
    mesq l("I AM DOING FINE!");
    next;
    mesq l("HEY, WHAT'S YOUR NAME?!");
    next;
    restorecam;

    menu
         strcharinfo(0) + ".", -,
         l("Maybe you can come down to talk?"), -;

    setcamnpc;
    mes "";
    mesn;
    mesq l("WHAT DID YOU SAY?!");
    next;
    mesq l("I DO NOT UNDERSTAND!");
    next;
    restorecam;

    menu
        l("I said... WHY DON'T YOU COME DOWN TO TALK?"), l_GoDown,
        l("I SAID, SEE YOU LATER!"), l_Bye,
        l("I'M CALLED, @@!", strcharinfo(0)), -;

    setcamnpc;
    mes "";
    mesn;
    mesq strcharinfo(0) + "? " + l("THAT'S A NICE NAME!");
    next;
    mesq l("SO WHAT'S UP?! WHAT ARE YOU DOING?!");
    next;
    restorecam;

    menu
        l("I'm looking for Gugli, where is he?"), -,
        l("I need to go, sorry."), l_Bye;

    setcamnpc;
    mes "";
    mesn;
    mesq l("OH, LOOK THERE!");
    setcamnpc "Gugli";
    next;
    mesq l("HE IS AT THE BOTTOM OF THE SHIP, DOWN THE STAIRS. YOU CAN'T MISS HIM!");
    restorecam;

    close;

l_GoDown:
    setcamnpc;
    mes "";
    mesn;
    mesq l("I WOULD LOVE TO!");
    next;
    mesq l("BUT I CAN'T, I NEED TO KEEP AN EYE ON THE SEA, TO WARN THE CREW IF THERE HAPPENS TO BE SOME PIRATES AROUND!");
    next;
    mesq l("I ALSO AVOID THE DANGEROUS ANIMALS!");
    next;
    restorecam;

    menu
        l("Have you seen anything dangerous?"), -,
        l("Interesting... I'll leave you to your task then!"), l_Bye;

    mes "";
    mesn;

    if (@q > 0) goto l_DangerAround;

    setcamnpc;
    mesq l("Oh no, but I've noticed a weird light on the other edge of this island, I wonder what it can be...");

    close;

l_Bye:
    setcamnpc;
    mes "";
    mesn;
    mesq l("FINE, BYE!");

    close;

l_DangerAround:
    setcamnpc;
    mesq l("NOTHING ELSE OTHER THAN SHARKS AND AN ODD LIGHT!");

    close;

l_Quit:
    close;

}

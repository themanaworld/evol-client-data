// Evol scripts.
// Authors:
//    Hal9000
//    Qwerty Dragon
//    Reid
// Description:
//    Sailor part of the Gugli's quest.
//    Croconut box.

000-1.gat,54,90,0,1	script	Gulukan	113;2,{
    set @g, getq(ShipQuests_Gugli);
    if (@g == 0) goto L_NoTask;

    set @q, getq(ShipQuests_Gulukan);
    if (@q == 1) goto L_Func;

    mesn;
    mesq l("Yeye @@!", strcharinfo(0));
    next;
    mesq l("How are you feeling, now that you walk on steady land? Yeye better?");
    next;

    menu
        l("Wow, it seems everyone knows my name!"), -,
        l("Well, it's not bad to finally feel something beneath my feet."), L_Continue,
        l("Not so good actually... I would feel better on Nard's ship."), L_Quit;

L_Name:
    mes "";
    mesn;
    mesq l("You know, yeyes love to chat while working.");
    next;

goto L_Continue2;

L_Continue:
    mes "";
    mesn;

L_Continue2:
    mesq lg("Are you yaying here to explore the island?");
    next;

    menu
        lg("Actually Gugli asked me if I would help you bring him some of your boxes."), -,
        l("Yes, indeed. I prefer to explore places I have never been to, before doing anything else."), L_Quit;

    mes "";
    mesn;
    mesq l("Gugli? Oh I see. Yes, Gugli yeye pretty used to shout orders to everybody down here.");
    next;
    mesq l("Anyway I am glad I can give you some of these yoyoxs.");
    next;

    set @item, 715;
    set @count, 1;
    callfunc "InventoryPlace", 715, 1;

    setq ShipQuests_Gulukan, 1;
    getitem "CroconutBox", 1;

    mes "";
    mesn;
    mesq lg("Thank you my friend.");

    close;

L_Func:
    mesn;

    set @q, rand(0, 400) / 100;
    if (@q == 0) goto L_RandomA;
    if (@q == 1) goto L_RandomB;
    if (@q > 1) goto L_RandomC;

L_RandomA:
    mesq l("Hey.");
    next;
    mesq l("What did Gugli yayaid about the box? Was it ok?");
    next;

    menu
        l("It's ok."), L_Fine,
        l("He needs more food."), -;

    mes "";
    mesn;
    mesq lg("Yeye sure? I'll yaye some more food in the next box then.");

    close;

L_Fine:
    mes "";
    mesn;
    mesq l("Alright! Yoyone more box and it'll be good.");

    close;

L_RandomB:
    mesq l("Thanks for the help!");
    next;
    mesq l("These yeye boxes are way too heavy to be carried alone onto the ship.");

    close;

L_RandomC:
    mesq l("I think that yeye's soon be done. Yeye'll soon have a full box of @@s!", getitemlink("CrocClaw"));
    next;
    mesq l("And you? How's it yaying on your side?");
    next;

    menu
        rif(getq(ShipQuests_Gugli) > 1, l("Task is done.")), L_TaskDone,
        rif(getq(ShipQuests_Gulukan) == 1 && (getq(ShipQuests_Ale) == 0 || getq(ShipQuests_Astapolos) == 0 || getq(ShipQuests_Jalad) == 0 || getq(ShipQuests_QMuller) == 0 || getq(ShipQuests_Tibbo) == 0), l("I'm still busy, I need to find the other sailors.")), -;

    mes "";
    mesn;
    mesq l("Who are yeye looking for?");
    next;

    menu
        rif(getq(ShipQuests_Ale) == 0, l("Ale.")), L_Top,
        rif(getq(ShipQuests_Astapolos) == 0, l("Astapolos.")), L_Top,
        rif(getq(ShipQuests_Jalad) == 0, l("Jalad.")), L_Top,
        rif(getq(ShipQuests_QMuller) == 0, l("Q'Muller.")), L_Top,
        rif(getq(ShipQuests_Tibbo) == 0, l("Tibbo.")), L_Bottom,
        l("Nobody."), L_Quit;

L_Bottom:
    mes "";
    mesn;
    mesq l("Yeye seen this yoiis at the bottom of the island, check around.");

    close;

L_Top:
    mes "";
    mesn;
    mesq l("Yeye seen this yoiis at the top of the island.");
    next;
    mesq l("You yoiis should walk to the north.");

    close;

L_NoTask:
    mes "";
    mesn;
    mesq l("*Uff*... And another box of @@s for our starving colleagues is ready.", getitemlink("Croconut"));

    close;

L_TaskDone:
    mes "";
    mesn;
    mesq lg("That's perfect, yoiis.");

    close;

L_Quit:
    closedialog;
    close;
}
